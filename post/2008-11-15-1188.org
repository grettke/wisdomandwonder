#+POSTID: 1188
#+DATE: [2008-11-15 Sat 04:17]
#+OPTIONS: toc:nil num:nil todo:nil pri:nil tags:nil ^:nil TeX:nil
#+CATEGORY: Link
#+TAGS: Emacs, Ide, Programming Language, elisp
#+TITLE: Toggle between Vertical and Horizontal Windows Splitting

On [[http://groups.google.com/group/gnu.emacs.help/browse_thread/thread/e76147ed69fd1ee9#][gnu.emacs.help]]:


#+BEGIN_QUOTE
  
Requested: Function that toggles between vertical and horizontal split layout of currently defined windows preferrably preserving splitting ratio. 

#+END_QUOTE





#+BEGIN_EXAMPLE
    
(defun my-toggle-window-split ()
  "Vertical split shows more of each line, horizontal split shows
more lines. This code toggles between them. It only works for
frames with exactly two windows."
  (interactive)
  (if (= (count-windows) 2)
      (let* ((this-win-buffer (window-buffer))
             (next-win-buffer (window-buffer (next-window)))
             (this-win-edges (window-edges (selected-window)))
             (next-win-edges (window-edges (next-window)))
             (this-win-2nd (not (and (<= (car this-win-edges)
                                         (car next-win-edges))
                                     (<= (cadr this-win-edges)
                                         (cadr next-win-edges)))))
             (splitter
              (if (= (car this-win-edges)
                     (car (window-edges (next-window))))
                  'split-window-horizontally
                'split-window-vertically)))
        (delete-other-windows)
        (let ((first-win (selected-window)))
          (funcall splitter)
          (if this-win-2nd (other-window 1))
          (set-window-buffer (selected-window) this-win-buffer)
          (set-window-buffer (next-window) next-win-buffer)
          (select-window first-win)
          (if this-win-2nd (other-window 1)))))) 

#+END_EXAMPLE



Thanks [[http://www.mygooglest.com/fni/index.html][Fabrice]].



